# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

subsystem_name = "ai"
part_name = "intelligent_voice_framework"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

copy_taihe_idl("copy_intellVoice_taihe") {
  sources = [
    "idl/ohos.ai.intelligentVoice.context.taihe",
    "idl/ohos.ai.intelligentVoice.taihe",
  ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_intellVoice_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.ai.intelligentVoice.ani.cpp",
    "$taihe_generated_file_path/src/ohos.ai.intelligentVoice.abi.c",
  ]
}

taihe_shared_library("intellVoice_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  include_dirs = [
    "inc",
    "../../interfaces/inner_api/native",
    "../../services/intell_voice_service/inc",
    "../../services/intell_voice_engine/proxy",
    "../../utils",
  ]

  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/intell_voice_manager_taihe.cpp",
    "src/wakeup_manager_taihe.cpp",
    "src/enroll_intell_voice_engine_taihe.cpp",
    "src/wakeup_intell_voice_engine_taihe.cpp",
    "src/intellvoice_taihe_utils.cpp",
    "src/enroll_intell_voice_engine_callback_taihe.cpp",
    "src/intell_voice_update_callback_taihe.cpp",
  ]

  cflags = [
    "-Wno-error=unused-parameter",
    "-DHILOG_ENABLE",
    "-DENABLE_DEBUG",
  ]

  deps = [
    ":run_taihe",
    "../../frameworks/native:intellvoice_native",
    "../../services:intell_voice_proxy",
  ]

  external_deps = [
    "ability_base:want",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "drivers_interface_intelligent_voice:intell_voice_engine_idl_headers_1.0",
    "drivers_interface_intelligent_voice:intell_voice_engine_idl_headers_1.1",
    "drivers_interface_intelligent_voice:intell_voice_engine_idl_headers_1.2",
    "hilog:libhilog",
    "ipc:ipc_core",
    "napi:ace_napi",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]
}

generate_static_abc("intelligent_voice_framework_taihe_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.ai.intelligentVoice.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/intelligent_voice_framework_taihe_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("intelligent_voice_framework_etc") {
  source = "$target_out_dir/intelligent_voice_framework_taihe_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [
    ":intelligent_voice_framework_taihe_abc"
  ]
}

group("intelligent_voice_framework_taihe") {
  deps = [
    ":intellVoice_taihe",
    ":intelligent_voice_framework_etc",
  ]
}

group("intelligent_voice_framework_taihe_gen_only") {
  deps = [ ":run_taihe" ]
}